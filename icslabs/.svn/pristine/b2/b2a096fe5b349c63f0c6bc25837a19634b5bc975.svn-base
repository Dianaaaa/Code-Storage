#/* $begin ncopy-ys */
##################################################################
# ncopy.ys - Copy a src block of len words to dst.
# Return the number of positive words (>0) contained in src.
#
# Include your name and ID here.
## QianXingyue 516072910066
#
# Describe how and why you modified the baseline code.
# 1. Reduce jump misprediction
# 2. Change addq and subq instructions to iaddq instructions. and it also change CC, which can be used as jmp Cnd. 
# 3. 8 * 8 loop rolling for the number of datas greater than 8
# 4. 2 * 2 loop rolling for the number of datas less than 8
# 5. Do read and store at different time (store after count-process)
##################################################################
# Do not modify this portion
# Function prologue.
# %rdi = src, %rsi = dst, %rdx = len
ncopy:

##################################################################
# You can modify this portion
	# Loop header
	xorq %rax,%rax		# count = 0;

	iaddq $-8, %rdx
	jmp Check8

Read:	
	mrmovq (%rdi), %r8	# read val from src...
	mrmovq 8(%rdi), %r9
	mrmovq 16(%rdi), %r10
	mrmovq 24(%rdi), %r11
	mrmovq 32(%rdi), %r12
	mrmovq 40(%rdi), %r13
	mrmovq 48(%rdi), %r14
	mrmovq 56(%rdi), %rcx
	
	andq %r8, %r8		# accumulating count
	jle Case101		#Case1ab: a is the number of 1, and b is the number of register that has been anded.
	
	andq %r9, %r9
	jle Case112
Case122:
	andq %r10, %r10
	jle Case123
Case133:
	andq %r11, %r11
	jle Case134
Case144:
	andq %r12, %r12
	jle Case145
Case155:
	andq %r13, %r13
	jle Case156
Case166:
	andq %r14, %r14
	jle Case167
Case177:
	andq %rcx, %rcx
	jle Case178
Case188:
	iaddq $8, %rax
	jmp Store8

Case101:
	andq %r9, %r9
	jle Case102
Case112:
	andq %r10, %r10
	jle Case113
Case123:
	andq %r11, %r11
	jle Case124
Case134:
	andq %r12, %r12
	jle Case135
Case145:
	andq %r13, %r13
	jle Case146
Case156:
	andq %r14, %r14
	jle Case157
Case167:
	andq %rcx, %rcx
	jle Case168
Case178:
	iaddq $7, %rax
	jmp Store8

Case102: 
	andq %r10, %r10
	jle Case103
Case113:
	andq %r11, %r11
	jle Case114
Case124:
	andq %r12, %r12
	jle Case125
Case135:
	andq %r13, %r13
	jle Case136
Case146:
	andq %r14, %r14
	jle Case147
Case157:
	andq %rcx, %rcx
	jle Case158
Case168:
	iaddq $6, %rax
	jmp Store8	
Case103:
	andq %r11, %r11
	jle Case104
Case114:
	andq %r12, %r12
	jle Case115
Case125:
	andq %r13, %r13
	jle Case126
Case136:
	andq %r14,%r14
	jle Case137
Case147:
	andq %rcx, %rcx
	jle Case148
Case158:
	iaddq $5, %rax
	jmp Store8
Case104:
	andq %r12, %r12
	jle Case105
Case115:
	andq %r13, %r13
	jle Case116
Case126:
	andq %r14, %r14
	jle Case127
Case137:
	andq %rcx, %rcx
	jle Case138
Case148:
	iaddq $4, %rax
	jmp Store8
Case105:
	andq %r13, %r13
	jle Case106
Case116:
	andq %r14, %r14
	jle Case117
Case127:
	andq %rcx, %rcx	
	jle Case128
Case138:
	iaddq $3, %rax
	jmp Store8
Case106:
	andq %r14, %r14
	jle Case107
Case117:
	andq %rcx, %rcx
	jle Case118
Case128:
	iaddq $2, %rax
	jmp Store8
Case107:
	andq %rcx, %rcx
	jle Case108
Case118:
	iaddq $1, %rax
Case108:

Store8:
	rmmovq %r8, (%rsi)	# ...and store it to dst
	rmmovq %r9, 8(%rsi)
	rmmovq %r10, 16(%rsi)
	rmmovq %r11, 24(%rsi)
	rmmovq %r12, 32(%rsi)
	rmmovq %r13, 40(%rsi)
	rmmovq %r14, 48(%rsi)
	rmmovq %rcx, 56(%rsi)
		
	iaddq $64, %rdi		# src+8
	iaddq $64, %rsi		# dst+8
	iaddq $-8, %rdx		# len-8
Check8:
	jge Read			# if so, goto Read:	

	iaddq $8, %rdx
	je Return 	

	iaddq $-2, %rdx 
	jmp Check2
Remain2more:
	mrmovq (%rdi), %r8	# read val from src...
	mrmovq 8(%rdi), %r9

	andq %r8, %r8
	jle Case0
	andq %r9, %r9
	jle Case10or01
	iaddq $2, %rax
	jmp Store2
Case0:
	andq %r9, %r9
	jle Case00
Case10or01:
	iaddq $1, %rax
Case00:
Store2:
	rmmovq %r8, (%rsi)	# ...and store it to dst
	rmmovq %r9, 8(%rsi)
	
	iaddq $16, %rdi		# src++
	iaddq $16, %rsi		# dst++
	iaddq $-2, %rdx		# len--
Check2:
	jge Remain2more

	iaddq $2, %rdx
	je Return
Remain:	
	mrmovq (%rdi), %r10	# read val from src...
	andq %r10, %r10		# val <= 0?
	rmmovq %r10, (%rsi)	# ...and store it to dst	
	jle Return		# if so, goto Return:
	iaddq $1, %rax		# count++	
Return:
##################################################################
# Do not modify the following section of code
# Function epilogue.
Done:
	ret
##################################################################
# Keep the following label at the end of your function
End:
#/* $end ncopy-ys */
